locals {
  aws_region     = "us-east-2"
  aws_account_id = "237918751012"
  app            = "gh-flow"
  org            = "me"
  details        = "Resource related to the gh-flow project"
  env            = "prod"
  created_by     = "ops-team"
}

remote_state {
  backend = "s3"
  generate = {
    path      = "remote_state.tf"
    if_exists = "overwrite"
  }
  config = {
    bucket  = "${local.org}-${local.app}-terraform-state"
    key     = "projects/${local.app}/${path_relative_to_include()}/terraform.tfstate"
    region  = local.aws_region
  }
}

generate "provider" {
  path      = "provider.tf"
  if_exists = "overwrite" # Allow modules to override provider settings
  contents  = <<EOF

terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "3.52.0"
    }
  }
}

# Configure the AWS Provider
provider "aws" {
  region              = "${local.aws_region}"
  allowed_account_ids = ["${local.aws_account_id}"]
}

EOF
}

generate "common_values" {
  path      = "common_variables.tf"
  if_exists = "overwrite"
  contents  = <<EOF

variable "tg_common_tags" {
  description = "Common tags for all AWS resources - (Generated by terragrunt)"
  default = {
    env       = "${local.env}"
    details   = "${local.details}"
    app       = "${local.app}"
    org       = "${local.org}"
    createdBy = "${local.created_by}"
  }
}

variable "tg_aws_region" {
  description = "AWS region to be deploy Intranet - (Generated by terragrunt)"
  default     = "${local.aws_region}"
}

variable "tg_app_name" {
  description = "Application name - (Generated by terragrunt)"
  default     = "${local.app}"
}

variable "tg_org_name" {
  description = "Organization name - (Generated by terragrunt)"
  default     = "${local.org}"
}


EOF
}
